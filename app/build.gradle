apply plugin: 'com.android.application'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd-HH-mm')
    return formattedDate
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.jikexueyuan.animationseries"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName APP_VERSION
    }

    signingConfigs {
        signingConfig {
            if("true".equals(IS_JENKINS)){
                storeFile file(JENKINS_KEY_STORE)
            }else {
                storeFile file(LOCAL_KEY_STORE)
            }
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storePassword STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /**多渠道打包*/
    productFlavors {

        channel{}
        channel1{}
        channel2{}

        productFlavors.all {
            flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }

    }

    //修改生成的apk名字及输出文件夹
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            //新名字
            def newName
            //时间戳
            def timeNow
            //输出文件夹
            def outDirectory
            //是否为Jenkins打包，输出路径不同
            if ("true".equals(IS_JENKINS)) {
                timeNow = BUILD_TIME
                //BUILD_PATH为服务器输出路径
                outDirectory = BUILD_PATH_JENKINS
                newName = 'AnimationSeries-v' + APP_VERSION + '-' + variant.productFlavors[0].name + '-' + variant.buildType.name + '.apk'
//                newName = 'gradle.apk'
            } else {
                //本机打包输出在本机路径
                timeNow = getDate()
//                outDirectory = output.outputFile.getParent()
                outDirectory = BUILD_PATH_LOCAL
                if ('debug'.equals(variant.buildType.name)) {
                    newName = "AnimationSeries-v${APP_VERSION}-debug.apk"
                } else {
                    newName = 'AnimationSeries-v' + APP_VERSION + '-' + variant.productFlavors[0].name + '-' + variant.buildType.name + '.apk'
                }
            }
            output.outputFile = new File(outDirectory+'/'+timeNow, newName)
//            output.outputFile = new File(outDirectory, newName)
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}
